@Metadata.layer: #CORE

@UI.headerInfo:
{
    typeName: 'Movie',
    typeNamePlural: 'Movies',
    title.value: 'Title',
   description.value: 'PublishingYear',
   imageUrl: 'ImageUrl'
}
@UI.presentationVariant: [{sortOrder: [{ by: 'AverageRating', direction: #DESC }]}]

@UI.lineItem: [ { criticality: 'StatusCriticality'  } ]
annotate entity ZC_05_Movie with
{

  @UI.facet: [ { position: 10, type: #FIELDGROUP_REFERENCE, label: 'Movie Details', targetQualifier: 'DETAILS' },
                 { position: 20, type: #FIELDGROUP_REFERENCE, label: 'Administrative Data', targetQualifier: 'ADMIN' },
                 { position: 30, type: #LINEITEM_REFERENCE, label: 'Ratings', targetElement: '_Rating' } ]

      /* Actions */
  @UI.lineItem: [{ position: 10, type: #FOR_ACTION, label: 'Rate Movie', dataAction: 'rateMovie'}]
  @UI.identification: [{ position: 10, type: #FOR_ACTION, label: 'Rate Movie', dataAction: 'rateMovie'}]

  @UI.fieldGroup: [{ position: 10, qualifier: 'ADMIN' }]
  MovieUuid;
  @UI.lineItem: [ { position: 10 } ]
  @UI.fieldGroup: [{ position: 10, qualifier: 'DETAILS' }]
  Title;
  @UI.lineItem: [ { position: 20 } ]
  @UI.selectionField: [ { position: 10 } ]
  @UI.fieldGroup: [{ position: 20, qualifier: 'DETAILS' }]
  Genre;
  @UI.lineItem: [ { position: 40 } ]
  @UI.selectionField: [ { position: 20 } ]
  @UI.fieldGroup: [{ position: 40, qualifier: 'DETAILS' }]
  PublishingYear;
  @UI.lineItem: [ { position: 30 } ]
  @UI.selectionField: [ { position: 30 } ]
  @UI.fieldGroup: [{ position: 30, qualifier: 'DETAILS' }]
  RuntimeInMin;
  @UI.lineItem: [ { position: 50 } ]
  @UI.fieldGroup: [{ position: 50, qualifier: 'DETAILS' }]
  ImageUrl;

  @UI.fieldGroup: [{ position: 20, qualifier: 'ADMIN' }]
  CreatedAt;
  @UI.fieldGroup: [{ position: 30, qualifier: 'ADMIN' }]
  CreatedBy;
  @UI.fieldGroup: [{ position: 40, qualifier: 'ADMIN' }]
  LastChangedAt;
  @UI.fieldGroup: [{ position: 50, qualifier: 'ADMIN' }]
  LastChangedBy;

  @UI.lineItem: [ { position: 45 } ]
  @UI.selectionField: [ { position: 25 } ]
  @UI.fieldGroup: [{ position: 45, qualifier: 'DETAILS' }]
  AverageRating;

  @UI.hidden: true
  StatusCriticality;

}